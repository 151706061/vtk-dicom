# This module is provided as DICOM_USE_FILE by DICOMConfig.cmake.  It can
# be INCLUDEd in a project to load the needed compiler and linker settings.

if(NOT DICOM_USE_FILE_INCLUDED)
  set(DICOM_USE_FILE_INCLUDED 1)

  # Dependencies
  set(VTK_DIR ${DICOM_VTK_DIR})
  find_package(VTK)
  if(VTK_FOUND)
    include(${VTK_USE_FILE})
  else()
    message(FATAL_ERROR
      "VTK not found in DICOM_VTK_DIR=\"${DICOM_VTK_DIR}\".")
  endif()

  if(DICOM_USE_GDCM)
    set(GDCM_DIR ${DICOM_GDCM_DIR})
    find_package(GDCM)
    if(GDCM_FOUND)
      include(${GDCM_USE_FILE})
    else()
      message(FATAL_ERROR
        "GDCM not found in DICOM_GDCM_DIR=\"${DICOM_GDCM_DIR}\".")
    endif()
  endif()

  if(DICOM_USE_DCMTK)
    find_package(DCMTK)
    if(NOT DCMTK_FOUND)
      message(FATAL_ERROR "DCMTK not found or incomplete.")
    endif()
    set(DCMTK_LIBS ${DCMTK_LIBRARIES})
    include_directories(${DCMTK_INCLUDE_DIRS})
  endif()

  if(NOT DICOM_USE_VTKZLIB)
    find_package(ZLIB REQUIRED)
    if(ZLIB_FOUND)
      include_directories("${ZLIB_INCLUDE_DIR}")
    endif()
  endif()

  # Update CMAKE_MODULE_PATH so includes work.
  set(CMAKE_MODULE_PATH
    ${CMAKE_MODULE_PATH}
    ${DICOM_CMAKE_DIR})

  # Add compiler flags needed to use DICOM.
  set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} ${DICOM_REQUIRED_C_FLAGS}")
  set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${DICOM_REQUIRED_CXX_FLAGS}")

  # Add include directories needed to use DICOM.
  include_directories(${DICOM_INCLUDE_DIRS})

endif(NOT DICOM_USE_FILE_INCLUDED)
